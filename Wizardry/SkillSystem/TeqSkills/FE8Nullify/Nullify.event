#ifndef HAX_SERW_NULLFY
#define HAX_SERW_NULLFY

// FE8 Nullify, with potential weapon effectiveness and Slayer overhaul
// By Tequila

#ifndef _FE8_
    ERROR "You're not assembling to an FE8 ROM!"
#endif

#include "EAstdlib.event"
#include "Extensions/Hack Installation.txt"

#ifndef FreeSpace
    #define FreeSpace 0xB2A610
    ORG FreeSpace
#endif

#ifndef __SLAYER_AND_EFFECTIVENESS_REWORK__
	//we merely add Slayer and Nullify checks as necessary

	PUSH
	
	//Slayer
	ORG $16C8A
	SHORT 0x46C0
	callHack_r3(Normal_Slayer)
	SHORT 0x2900 0xD000 //cmp r1,#0; beq nextLine
	
	//Weapon effectiveness
	ORG $16BFA
	SHORT 0x4D06 //ldr r5,=ItemTable
	callHack_r1(Normal_Effectiveness)
	SHORT 0x46C0 0x46C0 0x46C0
	
	POP
	
	ALIGN 4
	Normal_Slayer:
	#incbin "NormalSlayer.dmp"
	POIN SkillTester
	WORD SlayerID NullifyID
	
	ALIGN 4
	Normal_Effectiveness:
	#incbin "NormalEffectiveness.dmp"
	POIN SkillTester
	WORD NullifyID
	
#else // __SLAYER_AND_EFFECTIVENESS_REWORK__
	// OVERHAUL, BABY

	#ifndef SlayerEffectivenessReworkMacrosDefined
		#define SlayerEffectivenessReworkMacrosDefined

		#define SlayerTableEntry(aClassId, aTypeList, aCoeffTimes2) "BYTE aClassId aCoeffTimes2; SHORT aTypeList"
		#define ItemEffectivenessPtr(aItemId, aEffectivenessOffset) "PUSH; ORG ItemTable + 0x24*aItemId + 0x10; POIN aEffectivenessOffset; POP"
		#define SetWeaponEffectiveness(aTypeList, aCoeffTimes2) "BYTE 0 aCoeffTimes2; SHORT aTypeList"
		#define SetProtection(aTypeList) "SHORT 0 aTypeList"
	#endif // SlayerEffectivenessReworkMacrosDefined

	PUSH
	
	//Modify the part that multiplies weapon might for effective weapons and slayer
	ORG $2AAEC
	#incbin "AddWeaponMight.dmp"
	
	//slayer redux
	ORG $16C88
	jumpToHack(Redone_Slayer)
	
	//effectiveness redux
	ORG $16BEC
	jumpToHack(Redone_Effectiveness)
	
	POP
	
	ALIGN 4
	Redone_Slayer:
	#incbin "RedoneSlayer.dmp"
	POIN SkillTester
	WORD SlayerID
	WORD NullifyID
	POIN SlayerTable
	
	ALIGN 4
	Redone_Effectiveness:
	#incbin "RedoneEffectiveness.dmp"
	POIN SkillTester
	WORD NullifyID
	
	ALIGN 4
	SlayerTable:
	SlayerTableEntry(Bishop, EFF_MONSTER, 6)
	SlayerTableEntry(Bishop_F, EFF_MONSTER, 6)
	SlayerTableEntry(Ranger_F, EFF_MONSTER, 6)
	WORD 0 //terminator
	
	//Change existing effective items (class weaknesses are already part of the class data table)
	ItemEffectivenessPtr(Rapier, LordWeaponEffectiveness)
	ItemEffectivenessPtr(Armorslayer, ArmorEffectiveness)
	ItemEffectivenessPtr(Wyrmslayer, DragonEffectiveness)
	ItemEffectivenessPtr(Zanbato, HorseEffectiveness)
	ItemEffectivenessPtr(Horseslayer, HorseEffectiveness)
	ItemEffectivenessPtr(Halberd, HorseEffectiveness)
	ItemEffectivenessPtr(Hammer, ArmorEffectiveness)
	ItemEffectivenessPtr(Swordslayer, SwordEffectiveness)
	ItemEffectivenessPtr(IronBow, FlierEffectiveness)
	ItemEffectivenessPtr(SteelBow, FlierEffectiveness)
	ItemEffectivenessPtr(SilverBow, FlierEffectiveness)
	ItemEffectivenessPtr(PoisonBow, FlierEffectiveness)
	ItemEffectivenessPtr(KillerBow, FlierEffectiveness)
	ItemEffectivenessPtr(BraveBow, FlierEffectiveness)
	ItemEffectivenessPtr(ShortBow, FlierEffectiveness)
	ItemEffectivenessPtr(Longbow, FlierEffectiveness)
	ItemEffectivenessPtr(Ballista, FlierEffectiveness)
	ItemEffectivenessPtr(IronBallista, FlierEffectiveness)
	ItemEffectivenessPtr(KillerBallista, FlierEffectiveness)
	ItemEffectivenessPtr(Excalibur, SacredWeaponEffectiveness)
	ItemEffectivenessPtr(Luce, SacredWeaponEffectiveness)
	ItemEffectivenessPtr(DragonAxe, DragonEffectiveness)
	ItemEffectivenessPtr(Reginleif, LordWeaponEffectiveness)
	ItemEffectivenessPtr(ShadowKiller, MonsterEffectiveness)
	ItemEffectivenessPtr(BrightLance, MonsterEffectiveness)
	ItemEffectivenessPtr(Fiendcleaver, MonsterEffectiveness)
	ItemEffectivenessPtr(BeaconBow, FlierAndMonster1)
	ItemEffectivenessPtr(Sieglinde, SacredWeaponEffectiveness)
	ItemEffectivenessPtr(Ivaldi, SacredWeaponEffectiveness)
	ItemEffectivenessPtr(DragonSpear, DragonEffectiveness)
	ItemEffectivenessPtr(Vidofnir, SacredWeaponEffectiveness)
	ItemEffectivenessPtr(Audhulma, SacredWeaponEffectiveness)
	ItemEffectivenessPtr(Siegmund, SacredWeaponEffectiveness)
	ItemEffectivenessPtr(Garm, SacredWeaponEffectiveness)
	ItemEffectivenessPtr(Nidhogg, FlierAndMonster2)
	ItemEffectivenessPtr(HeavySpear, ArmorEffectiveness)
	ItemEffectivenessPtr(WindSword, FlierEffectiveness)
	ItemEffectivenessPtr(Dragonstone, MonsterEffectiveness)
	ItemEffectivenessPtr(Aircalibur, FlierEffectiveness)
	
	//Protector item (macro still works, even if it's wrongly named)
	ItemEffectivenessPtr(DelphiShield, FlierProtection)
	
	LordWeaponEffectiveness:
	SetWeaponEffectiveness(EFF_ARMOR|EFF_SWORD, 6)
	WORD 0 //terminator
	ArmorEffectiveness:
	SetWeaponEffectiveness(EFF_ARMOR, 6)
	WORD 0
	HorseEffectiveness:
	SetWeaponEffectiveness(EFF_HORSE, 6)
	WORD 0
	DragonEffectiveness:
	SetWeaponEffectiveness(EFF_DRAGON, 6)
	WORD 0
	FlierEffectiveness:
	SetWeaponEffectiveness(EFF_FLIER, 6)
	WORD 0
	SwordEffectiveness:
	SetWeaponEffectiveness(EFF_SWORD, 6)
	WORD 0
	MonsterEffectiveness:
	SetWeaponEffectiveness(EFF_MONSTER, 6)
	WORD 0
	SacredWeaponEffectiveness:
	SetWeaponEffectiveness(EFF_MONSTER, 4)
	WORD 0
	FlierAndMonster1: //Beacon bow
	SetWeaponEffectiveness(EFF_FLIER|EFF_MONSTER, 6)
	WORD 0
	FlierAndMonster2: //Nidhogg
	SetWeaponEffectiveness(EFF_FLIER, 6)
	SetWeaponEffectiveness(EFF_MONSTER, 4)
	WORD 0
	
	FlierProtection:
	SetProtection(EFF_FLIER)
	
#endif // __SLAYER_AND_EFFECTIVENESS_REWORK__

#endif // HAX_SERW_NULLFY